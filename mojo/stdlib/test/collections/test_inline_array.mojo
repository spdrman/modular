# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #

from std.sys.info import size_of

from std.compile import compile_info
from std.hashlib import hash
from std.memory import (
    UnsafeMaybeUninit,
    is_trivially_copyable,
    is_trivially_destructible,
    is_trivially_movable,
)
from test_utils import (
    CopyCounter,
    DelRecorder,
    MoveCounter,
    MoveOnly,
    check_write_to,
)
from std.testing import assert_equal, assert_true, assert_false, TestSuite


def test_array_unsafe_get() raises:
    # Negative indexing is undefined behavior with unsafe_get
    # so there are not test cases for it.
    var arr: InlineArray[Int, 3] = [0, 0, 0]

    assert_equal(arr.unsafe_get(0), 0)
    assert_equal(arr.unsafe_get(1), 0)
    assert_equal(arr.unsafe_get(2), 0)

    arr[0] = 1
    arr[1] = 2
    arr[2] = 3

    assert_equal(arr.unsafe_get(0), 1)
    assert_equal(arr.unsafe_get(1), 2)
    assert_equal(arr.unsafe_get(2), 3)


def test_array_int() raises:
    var arr: InlineArray[Int, 3] = [0, 0, 0]

    assert_equal(arr[0], 0)
    assert_equal(arr[1], 0)
    assert_equal(arr[2], 0)

    arr[0] = 1
    arr[1] = 2
    arr[2] = 3

    assert_equal(arr[0], 1)
    assert_equal(arr[1], 2)
    assert_equal(arr[2], 3)

    # test indexing from end
    assert_equal(arr[len(arr) - 1], 3)
    assert_equal(arr[len(arr) - 2], 2)

    # test indexing from end with dynamic index
    var i = len(arr) - 1
    assert_equal(arr[i], 3)
    i -= 1
    assert_equal(arr[i], 2)

    var copy = arr.copy()
    assert_equal(arr[0], copy[0])
    assert_equal(arr[1], copy[1])
    assert_equal(arr[2], copy[2])

    var move = arr^
    assert_equal(copy[0], move[0])
    assert_equal(copy[1], move[1])
    assert_equal(copy[2], move[2])

    # fill element initializer
    var arr2 = InlineArray[Int, 3](fill=5)
    assert_equal(arr2[0], 5)
    assert_equal(arr2[1], 5)
    assert_equal(arr2[2], 5)

    var arr3: InlineArray[Int, 1] = [5]
    assert_equal(arr3[0], 5)

    @parameter
    def test_init_fill[
        size: Int, batch_size: Int, dt: DType
    ](arg: Scalar[dt]) raises:
        var arr = InlineArray[Scalar[dt], size].__init__[batch_size=batch_size](
            fill=arg
        )
        for i in range(size):
            assert_equal(arr[i], arg)

    @parameter
    def test_init_fill_scalars[
        *dts: DType, sizes: List[Int], batch_sizes: List[Int]
    ]() raises:
        comptime for current_batch_size in range(len(batch_sizes)):
            comptime for current_size in range(len(sizes)):
                comptime for current_type in range(dts.size):
                    test_init_fill[
                        sizes[current_size], batch_sizes[current_batch_size]
                    ](Scalar[dts[current_type]].MAX)

    test_init_fill_scalars[
        Int64.dtype,
        Int8.dtype,
        sizes=[1, 32, 64, 129, 256, 512, 768, 1000],
        batch_sizes=[1, 8, 32, 64, 128],
    ]()

    test_init_fill[2048, 512](Int64.MAX)
    test_init_fill[2048, 1](Int64.MAX)


def test_array_String() raises:
    var arr: InlineArray[String, 3] = ["hi", "hello", "hey"]

    assert_equal(arr[0], "hi")
    assert_equal(arr[1], "hello")
    assert_equal(arr[2], "hey")

    # Test mutating an array through its __getitem__
    arr[0] = "howdy"
    arr[1] = "morning"
    arr[2] = "wazzup"

    assert_equal(arr[0], "howdy")
    assert_equal(arr[1], "morning")
    assert_equal(arr[2], "wazzup")

    # test indexing from end
    assert_equal(arr[len(arr) - 1], "wazzup")
    assert_equal(arr[len(arr) - 2], "morning")

    var copy = arr.copy()
    assert_equal(arr[0], copy[0])
    assert_equal(arr[1], copy[1])
    assert_equal(arr[2], copy[2])

    var move = arr^
    assert_equal(copy[0], move[0])
    assert_equal(copy[1], move[1])
    assert_equal(copy[2], move[2])

    # fill element initializer
    var arr2 = InlineArray[String, 3](fill="hi")
    assert_equal(arr2[0], "hi")
    assert_equal(arr2[1], "hi")
    assert_equal(arr2[2], "hi")

    # size 1 array to prevent regressions in the constructors
    var arr3: InlineArray[String, 1] = ["hi"]
    assert_equal(arr3[0], "hi")


def test_array_int_pointer() raises:
    var arr: InlineArray[Int, 3] = [0, 10, 20]

    var ptr = arr.unsafe_ptr()
    assert_equal(ptr[0], 0)
    assert_equal(ptr[1], 10)
    assert_equal(ptr[2], 20)

    ptr[0] = 0
    ptr[1] = 1
    ptr[2] = 2

    assert_equal(arr[0], 0)
    assert_equal(arr[1], 1)
    assert_equal(arr[2], 2)

    assert_equal(ptr[0], 0)
    assert_equal(ptr[1], 1)
    assert_equal(ptr[2], 2)

    # We make sure it lives long enough
    _ = arr


def test_array_unsafe_assume_initialized_constructor_string() raises:
    var maybe_uninitialized_arr = InlineArray[UnsafeMaybeUninit[String], 3](
        uninitialized=True
    )
    maybe_uninitialized_arr[0].init_from("hello")
    maybe_uninitialized_arr[1].init_from("mojo")
    maybe_uninitialized_arr[2].init_from("world")

    var initialized_arr = InlineArray[String, 3](
        unsafe_assume_initialized=maybe_uninitialized_arr^
    )

    assert_equal(initialized_arr[0], "hello")
    assert_equal(initialized_arr[1], "mojo")
    assert_equal(initialized_arr[2], "world")

    # trigger a move
    var initialized_arr2 = initialized_arr^

    assert_equal(initialized_arr2[0], "hello")
    assert_equal(initialized_arr2[1], "mojo")
    assert_equal(initialized_arr2[2], "world")

    # trigger a copy
    var initialized_arr3 = initialized_arr2.copy()

    assert_equal(initialized_arr3[0], "hello")
    assert_equal(initialized_arr3[1], "mojo")
    assert_equal(initialized_arr3[2], "world")

    # We assume the destructor was called correctly, but one
    # might want to add a test for that in the future.


def test_array_contains() raises:
    var arr: InlineArray[String, 3] = ["hi", "hello", "hey"]
    assert_true("hi" in arr)
    assert_true(not "greetings" in arr)


def test_inline_array_runs_destructors() raises:
    """Ensure we delete the right number of elements."""
    var destructor_recorder = List[Int]()
    var ptr = UnsafePointer(to=destructor_recorder).as_immutable()
    comptime capacity = 32
    var inline_list: InlineArray[DelRecorder[ptr.origin], 4] = [
        DelRecorder(0, ptr),
        DelRecorder(10, ptr),
        DelRecorder(20, ptr),
        DelRecorder(30, ptr),
    ]
    _ = inline_list
    # This is the last use of the inline list, so it should be destroyed here,
    # along with each element.
    assert_equal(len(destructor_recorder), 4)
    assert_equal(destructor_recorder[0], 0)
    assert_equal(destructor_recorder[1], 10)
    assert_equal(destructor_recorder[2], 20)
    assert_equal(destructor_recorder[3], 30)


def test_unsafe_ptr() raises:
    comptime N = 10
    var arr = InlineArray[Int, 10](fill=0)
    for i in range(N):
        arr[i] = i

    var ptr = arr.unsafe_ptr()
    for i in range(N):
        assert_equal(arr[i], ptr[i])


def _test_size_of_array[current_type: Copyable, capacity: Int]() raises:
    """Testing if `size_of` the array equals capacity * `size_of` current_type.

    Parameters:
        current_type: The type of the elements of the `InlineList`.
        capacity: The capacity of the `InlineList`.
    """
    comptime size_of_current_type = size_of[current_type]()
    assert_equal(
        size_of[InlineArray[current_type, capacity]](),
        capacity * size_of_current_type,
    )


def test_size_of_array() raises:
    _test_size_of_array[Int, 32]()


def test_move() raises:
    """Test that moving an InlineArray works correctly."""

    # === 1. Check that the move constructor is called correctly. ===

    var arr: InlineArray[MoveCounter[Int], 3] = [{1}, {2}, {3}]
    var copied_arr = arr.copy()

    for i in range(len(arr)):
        # The elements were moved into the array
        assert_equal(arr[i].move_count, 1)

    var moved_arr = arr^

    for i in range(len(moved_arr)):
        # Check that the moved array has the same elements as the copied array
        assert_equal(copied_arr[i].value, moved_arr[i].value)
        # Check that the move constructor was called again for each element
        assert_equal(moved_arr[i].move_count, 2)

    # === 2. Check that the copy constructor is not called when moving. ===

    var arr2: InlineArray[CopyCounter[], 3] = [{}, {}, {}]
    for i in range(len(arr2)):
        # The elements were moved into the array and not copied
        assert_equal(arr2[i].copy_count, 0)

    var moved_arr2 = arr2^

    for i in range(len(moved_arr2)):
        # Check that the copy constructor was not called
        assert_equal(moved_arr2[i].copy_count, 0)

    # === 3. Check that the destructor is not called when moving. ===

    var del_counter = List[Int]()
    var del_counter_ptr = UnsafePointer(to=del_counter).as_immutable()
    var del_recorder = DelRecorder[del_counter_ptr.origin](0, del_counter_ptr)
    var arr3: InlineArray[DelRecorder[del_counter_ptr.origin], 1] = [
        del_recorder
    ]

    assert_equal(len(del_counter_ptr[]), 0)

    var moved_arr3 = arr3^

    assert_equal(len(del_counter_ptr[]), 0)

    _ = moved_arr3

    # Double check that the destructor is called when the array is destroyed
    assert_equal(len(del_counter_ptr[]), 1)
    _ = del_recorder
    _ = del_counter


def test_different_types() raises:
    """Test with different element types."""
    # Test with single element
    var single: InlineArray[Int, 1] = [42]
    var single_str = String(single)
    assert_equal(single_str, "[42]")

    # Test with default values
    var default_array = InlineArray[Int, 3](fill=0)
    var default_str = String(default_array)
    assert_equal(default_str, "[0, 0, 0]")

    # Test with filled array
    var filled_array = InlineArray[Int, 4](fill=99)
    var filled_str = String(filled_array)
    assert_equal(filled_str, "[99, 99, 99, 99]")


def test_write_to() raises:
    """Test Writable trait implementation."""
    var array: InlineArray[Int, 3] = [10, 20, 30]
    check_write_to(array, expected="[10, 20, 30]", is_repr=False)

    var string_array: InlineArray[String, 2] = ["a", "b"]
    check_write_to(string_array, expected="[a, b]", is_repr=False)

    var single_elem: InlineArray[Int, 1] = [42]
    check_write_to(single_elem, expected="[42]", is_repr=False)


def test_write_repr_to() raises:
    """Test write_repr_to implementation."""
    var array: InlineArray[Int, 3] = [1, 2, 3]
    check_write_to(
        array,
        expected="InlineArray[Int, 3]([Int(1), Int(2), Int(3)])",
        is_repr=True,
    )

    var single: InlineArray[Int, 1] = [1]
    check_write_to(
        single, expected="InlineArray[Int, 1]([Int(1)])", is_repr=True
    )


def test_inline_array_triviality() raises:
    assert_true(is_trivially_destructible[InlineArray[Int, 1]]())
    assert_true(is_trivially_copyable[InlineArray[Int, 1]]())
    assert_true(is_trivially_movable[InlineArray[Int, 1]]())

    assert_false(is_trivially_destructible[InlineArray[String, 1]]())
    assert_false(is_trivially_copyable[InlineArray[String, 1]]())
    assert_true(is_trivially_movable[InlineArray[String, 1]]())


def _return_array[copy: Bool = False]() -> InlineArray[Int32, 4]:
    var arr = InlineArray[Int32, 4](fill=0)

    comptime if copy:
        return arr.copy()
    else:
        return arr^


def test_inline_array_copy_and_move_llvm_ir() raises:
    def _test(ir: StringSlice) raises:
        assert_true("initializes((0, 16))" in ir)
        assert_false('asm sideeffect "nop"' in ir)

    var move_info = compile_info[
        _return_array[copy=False], emission_kind="llvm-opt"
    ]()
    _test(move_info.asm)
    var copy_info = compile_info[
        _return_array[copy=True], emission_kind="llvm-opt"
    ]()
    _test(copy_info.asm)


def test_inline_array_iter() raises:
    var arr: InlineArray[Int, 3] = [0, 1, 2]
    var s = 0
    for el in arr:
        s += el
    assert_equal(s, 3)

    for el in reversed(arr):
        s -= el
    assert_equal(s, 0)


def test_inline_array_iter_mut() raises:
    var arr: InlineArray[Int, 3] = [0, 1, 2]
    for ref el in arr:
        el += 1

    var s = 0
    for el in arr:
        s += el
    assert_equal(s, 6)


def _test_inline_array_iter_bounds[
    I: Iterator
](var array_iter: I, array_len: Int) raises:
    var iter = array_iter^

    for i in range(array_len):
        var lower, upper = iter.bounds()
        print(lower, upper, i)
        assert_equal(array_len - i, lower)
        assert_equal(array_len - i, upper.value())
        _ = trait_downcast_var[Movable & ImplicitlyDeletable](iter.__next__())

    var lower, upper = iter.bounds()
    assert_equal(0, lower)
    assert_equal(0, upper.value())


struct NonWritable(Copyable):
    """A simple type that does not conform to Writable."""

    var value: Int


def test_inline_array_eq() raises:
    var a: InlineArray[Int, 3] = [1, 2, 3]
    var b: InlineArray[Int, 3] = [1, 2, 3]
    var c: InlineArray[Int, 3] = [1, 2, 4]

    assert_true(a == b)
    assert_false(a == c)
    assert_false(a != b)
    assert_true(a != c)

    var x: InlineArray[Int, 1] = [42]
    var y: InlineArray[Int, 1] = [42]
    var z: InlineArray[Int, 1] = [0]
    assert_true(x == y)
    assert_true(x != z)

    var s1: InlineArray[String, 2] = ["hello", "world"]
    var s2: InlineArray[String, 2] = ["hello", "world"]
    var s3: InlineArray[String, 2] = ["hello", "mojo"]
    assert_true(s1 == s2)
    assert_true(s1 != s3)

    # Test arrays of different lengths.
    var d: InlineArray[Int, 4] = [1, 2, 3, 4]
    var e: InlineArray[Int, 4] = [1, 2, 3, 4]
    var f: InlineArray[Int, 4] = [1, 2, 3, 5]
    assert_true(d == e)
    assert_true(d != f)

    var g: InlineArray[Int, 5] = [10, 20, 30, 40, 50]
    var h: InlineArray[Int, 5] = [10, 20, 30, 40, 50]
    var i: InlineArray[Int, 5] = [10, 20, 99, 40, 50]
    assert_true(g == h)
    assert_true(g != i)


def test_inline_array_hash() raises:
    var a: InlineArray[Int, 3] = [1, 2, 3]
    var b: InlineArray[Int, 3] = [1, 2, 3]
    var c: InlineArray[Int, 3] = [1, 2, 4]

    # Equal arrays must have equal hashes.
    assert_equal(hash(a), hash(b))

    # Different arrays should (almost certainly) have different hashes.
    assert_true(hash(a) != hash(c))


def test_inline_array_conditional_conformances() raises:
    assert_true(conforms_to(InlineArray[Int, 3], Writable))
    assert_true(conforms_to(InlineArray[Int, 3], Equatable))
    assert_true(conforms_to(InlineArray[Int, 3], Hashable))
    assert_true(conforms_to(InlineArray[Int, 3], Copyable))
    assert_true(conforms_to(InlineArray[Int, 3], ImplicitlyCopyable))
    assert_true(conforms_to(InlineArray[Int, 3], ImplicitlyDeletable))
    assert_false(conforms_to(InlineArray[NonWritable, 3], Writable))


def test_inline_array_iter_bounds() raises:
    var arr: InlineArray[Int, 3] = [1, 2, 3]
    _test_inline_array_iter_bounds(iter(arr), len(arr))
    _test_inline_array_iter_bounds(reversed(arr), len(arr))


def test_inline_array_iter_owned() raises:
    var arr: InlineArray[Int, 3] = [10, 20, 30]
    var result = List[Int]()
    for elem in arr^:
        result.append(elem)

    assert_equal(len(result), 3)
    assert_equal(result[0], 10)
    assert_equal(result[1], 20)
    assert_equal(result[2], 30)


def test_inline_array_iter_owned_destroys_elements_if_not_consumed() raises:
    # Verify that creating and immediately dropping the iterator doesn't crash.
    var arr: InlineArray[Int, 3] = [1, 2, 3]
    var _ = arr^.__iter__()


def test_inline_array_iter_owned_destroys_elements_if_partially_consumed() raises:
    # Verify partial consumption followed by dropping doesn't crash.
    var arr: InlineArray[Int, 3] = [1, 2, 3]
    var it = arr^.__iter__()
    _ = it.__next__()  # consume one element
    _ = it^  # drop iterator with remaining elements


def test_inline_array_iter_owned_bounds() raises:
    var arr: InlineArray[Int, 3] = [1, 2, 3]
    var it = arr^.__iter__()
    assert_equal(it.bounds()[0], 3)
    _ = it.__next__()
    assert_equal(it.bounds()[0], 2)
    _ = it.__next__()
    assert_equal(it.bounds()[0], 1)
    _ = it.__next__()
    assert_equal(it.bounds()[0], 0)


def test_inline_array_move_only() raises:
    # `MoveOnly[Int]` is not `Copyable`; this exercises the conditional
    # conformance path of `InlineArray[T: Movable, size]`.
    assert_false(conforms_to(InlineArray[MoveOnly[Int], 2], Copyable))

    var arr: InlineArray[MoveOnly[Int], 3] = [
        MoveOnly[Int](0),
        MoveOnly[Int](1),
        MoveOnly[Int](2),
    ]
    assert_equal(arr[0], MoveOnly[Int](0))
    assert_equal(arr[1], MoveOnly[Int](1))
    assert_equal(arr[2], MoveOnly[Int](2))
    assert_equal(len(arr), 3)

    # `unsafe_get` is a non-copying accessor.
    assert_equal(arr.unsafe_get(2), MoveOnly[Int](2))


def main() raises:
    var suite = TestSuite.discover_tests[__functions_in_module()]()
    # TODO: skipped to work around MOCO-3749
    suite.skip[test_inline_array_copy_and_move_llvm_ir]()
    suite^.run()
