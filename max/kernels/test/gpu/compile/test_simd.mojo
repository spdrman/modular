# ===----------------------------------------------------------------------=== #
# Copyright (c) 2026, Modular Inc. All rights reserved.
#
# Licensed under the Apache License v2.0 with LLVM Exceptions:
# https://llvm.org/LICENSE.txt
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# ===----------------------------------------------------------------------=== #


from std.sys.info import _accelerator_arch, _is_sm_100x_or_newer

from std.gpu.host import get_gpu_target
from std.gpu.host.compile import _compile_code
from std.gpu.host.info import B200, GPUInfo, _is_sm10x_gpu
from std.testing import assert_false, assert_true


def test_operation[
    dtype: DType,
    target_arch: StaticString,
    op_fn: fn[width: Int](
        x: SIMD[dtype, width], y: type_of(x)
    ) raises -> type_of(x),
    op_name: StaticString,
]() raises:
    var scalar: String
    var pairwise: String
    var suffix: String

    # sm_80 does not support trivial add/sub/mul bfloat16 operations, but
    # these can be implemented using the FMA instruction. Verify that the
    # backend is using FMA and not falling back to widening the inputs to
    # float32.
    # sm_90 and later has wider support for bfloat16 operations.
    # sm_100 has support for f32x2 add/sub/mul/fma.
    var prefix: String

    comptime if target_arch == "sm_80" and dtype == DType.bfloat16:
        prefix = "fma.rn"
    else:
        prefix = String(op_name)

    comptime if dtype == DType.float16:
        suffix = ".f16"
    elif dtype == DType.float32:
        suffix = ".f32"
    else:
        suffix = ".bf16"

    scalar = prefix + suffix
    pairwise = scalar + "x2 "

    comptime target = get_gpu_target[target_arch]()
    assert_true(scalar in _compile_code[op_fn[width=1], target=target]())
    assert_true(pairwise in _compile_code[op_fn[width=2], target=target]())
    assert_true(pairwise in _compile_code[op_fn[width=8], target=target]())


def test_add[dtype: DType, target_arch: StaticString]() raises:
    def add[width: Int](x: SIMD[dtype, width], y: type_of(x)) -> type_of(x):
        return x + y

    test_operation[dtype, target_arch, add, "add"]()


def test_sub[dtype: DType, target_arch: StaticString]() raises:
    def sub[width: Int](x: SIMD[dtype, width], y: type_of(x)) -> type_of(x):
        return x - y

    test_operation[dtype, target_arch, sub, "sub"]()


def test_mul[dtype: DType, target_arch: StaticString]() raises:
    def mul[width: Int](x: SIMD[dtype, width], y: type_of(x)) -> type_of(x):
        return x * y

    test_operation[dtype, target_arch, mul, "mul"]()


def test_half_float_instruction_selection() raises:
    def test_operations[dtype: DType, target_arch: StaticString]() raises:
        test_add[dtype, target_arch]()
        test_sub[dtype, target_arch]()
        test_mul[dtype, target_arch]()

    def test_types[dtype: DType]() raises:
        test_operations[dtype, "sm_80"]()
        test_operations[dtype, "sm_90"]()

    test_types[DType.bfloat16]()
    test_types[DType.float16]()


def test_fma[dtype: DType]() raises:
    def fma[
        width: Int
    ](x: SIMD[dtype, width], y: type_of(x), z: type_of(x)) -> type_of(x):
        return x * y + z

    def fma_manual[
        width: Int
    ](x: SIMD[dtype, width], y: type_of(x), z: type_of(x)) -> type_of(x):
        return x.fma(y, z)

    comptime if dtype == DType.bfloat16:
        assert_true("fma.rn.bf16 " in _compile_code[fma[width=1]]())
        assert_true("fma.rn.bf16x2 " in _compile_code[fma[width=2]]())
        assert_true("fma.rn.bf16x2 " in _compile_code[fma[width=8]]())
    elif dtype == DType.float32:
        assert_true("fma.rn.f32 " in _compile_code[fma_manual[width=1]]())
        assert_true("fma.rn.f32x2 " in _compile_code[fma_manual[width=2]]())
        assert_true("fma.rn.f32x2 " in _compile_code[fma_manual[width=8]]())
    else:
        assert_true("fma.rn.f16 " in _compile_code[fma[width=1]]())
        assert_true("fma.rn.f16x2 " in _compile_code[fma[width=2]]())
        assert_true("fma.rn.f16x2 " in _compile_code[fma[width=8]]())


def test_cast() raises:
    def cast[
        src_type: DType, dst_type: DType, width: Int
    ](src: SIMD[src_type, width]) -> SIMD[dst_type, width]:
        return src.cast[dst_type]()

    assert_true(
        "cvt.rn.f16x2.f32"
        in _compile_code[
            cast[src_type=DType.float32, dst_type=DType.float16, width=4]
        ]()
    )
    assert_true(
        "cvt.rn.bf16x2.f32"
        in _compile_code[
            cast[src_type=DType.float32, dst_type=DType.bfloat16, width=4]
        ]()
    )
    assert_true(
        "cvt.f32.bf16"
        in _compile_code[
            cast[src_type=DType.bfloat16, dst_type=DType.float32, width=1]
        ]()
    )
    assert_true(
        "cvt.f32.bf16"
        in _compile_code[
            cast[src_type=DType.bfloat16, dst_type=DType.float32, width=4]
        ]()
    )


def main() raises:
    test_half_float_instruction_selection()

    test_fma[DType.bfloat16]()
    test_fma[DType.float16]()

    test_cast()

    comptime device = GPUInfo.from_name[_accelerator_arch()]()

    comptime if _is_sm10x_gpu(device):
        test_add[DType.float32, "sm_100"]()
        test_mul[DType.float32, "sm_100"]()
        test_fma[DType.float32]()
